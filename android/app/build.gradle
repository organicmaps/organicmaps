buildscript {
  repositories {
    google()
    mavenCentral()
    gradlePluginPortal()  // For ru.cian.huawei-plugin
  }
  //
  // The magic below is needed to disable Google Firebase Services during the build time.
  // Unfortunately, the only way to disable Gradle plugins is to add these hardcore switches to buildscript().
  //

  // Detect flavors from the task name.
  def taskName = getGradle().getStartParameter().getTaskRequests().toString().toLowerCase()
  def isFdroid = taskName.contains('fdroid')
  def isBeta = taskName.contains('beta')

  // Firebase Crashlytics compile-time feature flag: -Pfirebase=true|false
  def googleFirebaseServicesFlag = findProperty('firebase')
  // Enable Firebase for all beta flavors except fdroid only if google-services.json exists.
  def googleFirebaseServicesDefault = isBeta && !isFdroid && file("$projectDir/google-services.json").exists()
  ext.googleFirebaseServicesEnabled = googleFirebaseServicesFlag != null ?
      googleFirebaseServicesFlag == '' || googleFirebaseServicesFlag.toBoolean() :
      googleFirebaseServicesDefault

  dependencies {
    classpath 'com.android.tools.build:gradle:8.13.0'

    if (googleFirebaseServicesEnabled) {
      println('Building with Google Firebase Services')
      classpath 'com.google.gms:google-services:4.4.3'
      classpath 'com.google.firebase:firebase-crashlytics-gradle:3.0.6'
      classpath 'com.google.firebase:firebase-appdistribution-gradle:5.1.1'
    } else {
      println('Building without Google Firebase Services')
    }

    classpath('com.github.triplet.gradle:play-publisher:3.12.1')
    classpath('ru.cian.huawei-plugin:plugin:1.5.0')
  }
}

apply plugin: 'com.android.application'
if (googleFirebaseServicesEnabled) {
  apply plugin: 'com.google.gms.google-services'
  apply plugin: 'com.google.firebase.crashlytics'
  apply plugin: 'com.google.firebase.appdistribution'
}
apply plugin: 'com.github.triplet.play'
apply plugin: 'ru.cian.huawei-publish-gradle-plugin'

apply from: "${rootProject.rootDir}/groovy/common-config.gradle"
apply from: "${rootProject.rootDir}/groovy/permission-checker.gradle"

import com.github.triplet.gradle.androidpublisher.ReleaseStatus
import ru.cian.huawei.publish.ReleaseNote
import ru.cian.huawei.publish.ReleaseNotesExtension

def getCommitMessage() {
  return run(['git', '--no-pager', 'show', '-s', '--format=%s%n%n%b', 'HEAD']).trim()
}

project.ext.appId = 'app.organicmaps'
project.ext.appName = 'Organic Maps'

android {
  namespace = 'app.organicmaps'

  buildFeatures {
    dataBinding = true
    buildConfig = true
  }

  // Users are complaining that the app should be re-downloaded from the Play Store after changing the language.
  bundle {
    language {
      enableSplit = false
    }
  }

  defaultConfig {
    versionCode = rootProject.ext.versionCode
    versionName = rootProject.ext.versionName
    println('Version: ' + versionName)
    println('VersionCode: ' + versionCode)
    applicationId project.ext.appId
    buildConfigField 'String', 'SUPPORT_MAIL', '"android@organicmaps.app"'
    // Should be customized in flavors.
    buildConfigField 'String', 'REVIEW_URL', '""'
    resourceConfigurations += [project.ext.supportedLocalizations]

    base.archivesName = appName.replaceAll('\\s', '') + '-' + defaultConfig.versionCode

    ndk.debugSymbolLevel = 'full'
  }

  flavorDimensions += 'default'

  productFlavors {
    // 01 is a historical artefact, sorry.
    final int HUAWEI_VERSION_CODE_BASE = 01_00_00_00_00

    google {
      dimension 'default'
      versionName = android.defaultConfig.versionName + '-Google'
      buildConfigField 'String', 'SUPPORT_MAIL', '"googleplay@organicmaps.app"'
      buildConfigField 'String', 'REVIEW_URL', '"market://details?id=app.organicmaps"'
    }

    web {
      dimension 'default'
      applicationIdSuffix '.web'
      versionName = android.defaultConfig.versionName + '-Web'
      buildConfigField 'String', 'SUPPORT_MAIL', '"apk@organicmaps.app"'
    }

    fdroid {
      dimension 'default'
      versionName = android.defaultConfig.versionName + '-FDroid'
      buildConfigField 'String', 'SUPPORT_MAIL', '"fdroid@organicmaps.app"'
    }

    huawei {
      dimension 'default'
      versionName = android.defaultConfig.versionName + '-Huawei'
      versionCode = HUAWEI_VERSION_CODE_BASE + android.defaultConfig.versionCode
      buildConfigField 'String', 'SUPPORT_MAIL', '"huawei@organicmaps.app"'
      buildConfigField 'String', 'REVIEW_URL', '"appmarket://details?id=app.organicmaps"'
    }
  }

  playConfigs {
    googleRelease {
      enabled.set(true)
    }
  }

  splits.abi {
    boolean enabled = project.hasProperty('splitApk')
    println('Create separate apks: ' + enabled)
    enable = enabled
    reset()
    include 'x86', 'armeabi-v7a', 'arm64-v8a', 'x86_64'
    universalApk = true
  }

  def securityPropertiesFileExists = file('secure.properties').exists()
  if (securityPropertiesFileExists) {
    apply from: 'secure.properties'
  }

  signingConfigs {
    debug {
      storeFile file('debug.keystore')
      storePassword '12345678'
      keyAlias 'debug'
      keyPassword '12345678'
    }

    release {
      if (securityPropertiesFileExists) {
        println('The release signing keys are available')
        storeFile file(spropStoreFile)
        storePassword spropStorePassword
        keyAlias spropKeyAlias
        keyPassword spropKeyPassword
      } else {
        println('The release signing keys are unavailable')
      }
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.debug'   // Allows to install debug and release builds together
      versionNameSuffix '-debug'
      signingConfig = signingConfigs.debug
      resValue 'string', 'app_name', 'Debug Organic Maps'

      if (googleFirebaseServicesEnabled) {
        firebaseCrashlytics {
          nativeSymbolUploadEnabled true
        }
      }
    }

    release {
      signingConfig = signingConfigs.release
      minifyEnabled true
      shrinkResources = true
      // Includes the default ProGuard rules files that are packaged with the Android Gradle plugin.
      // To learn more, go to the documentation section about R8 configuration files.
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      resValue 'string', 'app_name', project.ext.appName

      if (googleFirebaseServicesEnabled) {
        firebaseCrashlytics {
          nativeSymbolUploadEnabled true
        }
      }
    }

    beta {
      applicationIdSuffix '.beta'
      versionNameSuffix '-beta'
      signingConfig = signingConfigs.release
      minifyEnabled true
      shrinkResources = true
      // Includes the default ProGuard rules files that are packaged with the Android Gradle plugin.
      // To learn more, go to the documentation section about R8 configuration files.
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      matchingFallbacks = ['debug', 'release']
      resValue 'string', 'app_name', 'Beta Organic Maps'

      if (googleFirebaseServicesEnabled) {
        firebaseCrashlytics {
          nativeSymbolUploadEnabled true
        }
        firebaseAppDistribution {
          // A new beta release is created for each commit.
          // Use the last commit message for the release notes.
          releaseNotes = getCommitMessage()
          groups = 'qa' // Notify only selected people.
          serviceCredentialsFile = "$projectDir/firebase-app-distribution.json"
        }
      }
    }
  }

  // We don't compress these extensions in assets/ because our random FileReader can't read zip-compressed files from apk.
  // TODO: Load all minor files via separate call to ReadAsString which can correctly handle compressed files in zip containers.
  androidResources {
    ignoreAssetsPattern = '!.svn:!.git:!.DS_Store:!*.scc:.*:<dir>_*:!CVS:!thumbs.db:!picasa.ini:!*~'
    noCompress = ['txt', 'bin', 'html', 'png', 'json', 'mwm', 'ttf', 'sdf', 'ui', 'config', 'csv', 'spv', 'obj']
  }

  compileOptions {
    coreLibraryDesugaringEnabled = true
  }
}

dependencies {
  implementation project(':sdk')
  // World maps
  googleImplementation project(':sdk:maps:world')
  huaweiImplementation project(':sdk:maps:world')

  coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'

  // Google Play Location Services
  webImplementation 'com.google.android.gms:play-services-location:21.3.0'
  googleImplementation 'com.google.android.gms:play-services-location:21.3.0'
  huaweiImplementation 'com.google.android.gms:play-services-location:21.3.0'
  // This is the microG project's re-implementation which is permissible on
  // F-droid because it's Apache-2.0.
  fdroidImplementation 'org.microg.gms:play-services-location:0.3.6.244735'

  // Google Firebase Services
  if (googleFirebaseServicesEnabled) {
    // Import the BoM for the Firebase platform.
    // firebase-crashlytics-ndk requires minSdkVersion 23 since 34.0.0
    // See https://firebase.google.com/support/release-notes/android#crashlytics-ndk_v20-0-0
    // Use 33.11.0 instead of 33.16.0 because of the https://github.com/firebase/firebase-android-sdk/issues/6993
    implementation platform('com.google.firebase:firebase-bom:33.11.0')
    // Add the dependencies for the Crashlytics and Analytics libraries
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-crashlytics-ndk'
  }

  // This line is added as a workaround for duplicate classes error caused by some outdated dependency:
  // > A failure occurred while executing com.android.build.gradle.internal.tasks.CheckDuplicatesRunnable
  // We don't use Kotlin, but some dependencies are actively using it.
  // See https://stackoverflow.com/a/75719642
  implementation 'androidx.core:core:1.17.0'
  implementation(platform('org.jetbrains.kotlin:kotlin-bom:2.2.20'))
  implementation 'androidx.annotation:annotation:1.9.1'
  implementation 'androidx.appcompat:appcompat:1.7.1'
  implementation 'androidx.car.app:app:1.8.0-alpha02'
  implementation 'androidx.car.app:app-projected:1.8.0-alpha02'
  implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
  implementation 'androidx.fragment:fragment:1.8.9'
  implementation 'androidx.preference:preference:1.2.1'
  implementation 'androidx.recyclerview:recyclerview:1.4.0'
  implementation 'androidx.work:work-runtime:2.10.5'
  implementation 'androidx.lifecycle:lifecycle-process:2.9.4'
  implementation 'com.google.android.material:material:1.13.0'
  // Fix for app/organicmaps/util/FileUploadWorker.java:14: error: cannot access ListenableFuture
  // https://github.com/organicmaps/organicmaps/issues/6106
  implementation 'com.google.guava:guava:33.5.0-android'
  implementation 'com.github.devnullorthrow:MPAndroidChart:3.2.0-alpha'

  // Test Dependencies
  testImplementation 'androidx.test.ext:junit:1.3.0'
  testImplementation 'org.mockito:mockito-core:5.20.0'
  testImplementation 'org.mockito:mockito-inline:5.2.0'
}

afterEvaluate {
  def allowedPermissions = [
      // location
      "name='android.permission.ACCESS_COARSE_LOCATION'",
      "name='android.permission.ACCESS_FINE_LOCATION'",
      "name='android.permission.ACCESS_LOCATION_EXTRA_COMMANDS'",
      // network
      "name='android.permission.INTERNET'",
      "name='android.permission.ACCESS_NETWORK_STATE'",
      // foreground services
      "name='android.permission.FOREGROUND_SERVICE'",
      "name='android.permission.FOREGROUND_SERVICE_LOCATION'",
      "name='android.permission.FOREGROUND_SERVICE_MEDIA_PLAYBACK'",
      "name='android.permission.FOREGROUND_SERVICE_DATA_SYNC'",
      "name='android.permission.FOREGROUND_SERVICE_SPECIAL_USE'",
      // notifications
      "name='android.permission.POST_NOTIFICATIONS'",
      // wake lock
      "name='android.permission.WAKE_LOCK'",
      // Android Auto
      "name='androidx.car.app.NAVIGATION_TEMPLATES'",
      "name='androidx.car.app.ACCESS_SURFACE'",
      // storage
      "name='android.permission.READ_EXTERNAL_STORAGE' maxSdkVersion='22'",
      // internal permissions
      // comes from androidx.work:work-runtime
      "name='android.permission.RECEIVE_BOOT_COMPLETED'",
      // all apps targeting Android 13 and higher automatically get RECEIVER_NOT_EXPORTED_PERMISSION flag added to their Manifest files
      // Regex to allow it for the following app packages:
      // app.organicmaps
      // app.organicmaps.(debug|beta)
      // app.organicmaps.web.(debug|beta)
      ~/name='app\.organicmaps(\.web)?(\.debug|\.beta)?\.DYNAMIC_RECEIVER_NOT_EXPORTED_PERMISSION'/
  ]
  registerCheckApkPermissionsTasks(project, allowedPermissions)
}

android.applicationVariants.all { variant ->
  def authorityValue = variant.applicationId + ".provider"
  def authority = "\"" + authorityValue + "\""
  variant.buildConfigField 'String', 'FILE_PROVIDER_AUTHORITY', authority
  def flavor = variant.getMergedFlavor()
  flavor.manifestPlaceholders += [FILE_PROVIDER_PLACEHOLDER: authorityValue]
  variant.resValue 'string', 'app_id', variant.applicationId
}

tasks.register("prepareGoogleReleaseListing") {
  group = "publishing"

  // Prepares Google Play metainfo from F-Droid metainfo.
  final sourceFlavor = 'fdroid'
  final targetFlavor = 'google'
  doLast {
    final sourceDir = new File("${projectDir}/src/$sourceFlavor/play/listings")
    final targetDir = new File("${projectDir}/src/$targetFlavor/play/listings")
    final sourceFiles = fileTree(dir: sourceDir,
        include: '**/*.txt', exclude: "**/*-${targetFlavor}.txt")
    sourceFiles.each { File sourceFile ->
      final locale = sourceFile.parentFile.getName()
      final targetLocaleDir = new File(targetDir, locale)
      if (!targetLocaleDir.isDirectory())
        targetLocaleDir.mkdirs()
      final targetFile = new File(targetLocaleDir, sourceFile.getName())
      // Override Google-specific values by using ${name}-google.txt files.
      final overrideFile = new File(sourceFile.getPath().replace('.txt', "-${targetFlavor}.txt"))
      targetFile.text = overrideFile.exists() ? overrideFile.text : sourceFile.text
    }
    copy {
      from "${projectDir}/../../screenshots/android"
      into targetDir
    }
  }
}

play {
  enabled.set(false)
  track.set('production')
  defaultToAppBundles.set(true)
  releaseStatus.set(ReleaseStatus.IN_PROGRESS)
  serviceAccountCredentials.set(file('google-play.json'))
}

huaweiPublish {
  instances {
    huaweiRelease {
      credentialsPath = "$projectDir/huawei-appgallery.json"
      buildFormat = 'aab'
      deployType = 'draft' // confirm manually
      def releaseDescriptions = []
      def localeOverride = [
          'am' : 'am-ET',
          'gu': 'gu_IN',
          'iw-IL': 'he_IL',
          'kn-IN': 'kn_IN',
          'ml-IN': 'ml_IN',
          'mn-MN': 'mn_MN',
          'mr-IN': 'mr_IN',
          'ta-IN': 'ta_IN',
          'te-IN': 'te_IN',
      ]
      def files = fileTree(dir: "$projectDir/src/fdroid/play/listings",
          include: '**/release-notes.txt')
      files.each { File file ->
        def path = file.getPath()
        def locale = file.parentFile.getName()
        locale = localeOverride.get(locale, locale)
        releaseDescriptions.add(new ReleaseNote(locale, path))
      }
      releaseNotes = new ReleaseNotesExtension(releaseDescriptions, true)
    }
  }
}
